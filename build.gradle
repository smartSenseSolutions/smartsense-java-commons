buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group = groupId
    version = packageVersion
}

subprojects {

    apply plugin: "java"

    sourceCompatibility = JavaVersion.VERSION_21

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

project(":commons-dao") {
    apply plugin: "maven-publish"
    apply plugin: "signing"
    publishing {
        publications {
            mavenJava(MavenPublication) {
                signing {
                    sign publishing.publications.mavenJava
                }
                artifact jar
                artifact tasks.javadocJar
                artifact tasks.sourcesJar

                repositories {
                    maven {
                        name = "Ossrh"
                        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                        credentials() {
                            username = ossrhUsername
                            password = ossrhPassword
                        }
                    }
                }

                pom {
                    name = 'Commons-Dao'
                    packaging 'jar'
                    description = 'Commons-Dao used for make easy implementation of spring data jpa.'
                    url = 'https://www.smartsensesolutions.com/'

                    organization {
                        name = 'smartsensesolutions.com'
                        url = 'https://www.smartsensesolutions.com/'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'dilip.dhankecha'
                            name = 'Dilip Dhankecha'
                            email = 'dilip.dhankecha@smartsensesolutions.com'
                        }
                        developer {
                            id = 'nitin.vavdiya'
                            name = 'Nitin Vavdiya'
                            email = 'nitin.vavdiya@smartsensesolutions.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/smartSenseSolutions/smartsense-java-commons.git'
                        developerConnection = 'scm:git:ssh://github.com/smartSenseSolutions/smartsense-java-commons.git'
                        url = 'https://github.com/smartSenseSolutions/smartsense-java-commons/'
                    }
                }

            }
        }
    }
}